SELECT ?language_name (SUM (?speakers) as ?total_speakers)
WHERE {
  #Base
  #Country pertenece a un continente
  ?country wdt:P361 ?continent .
  #Se habla un lenguaje
  ?country wdt:P2936 ?language .
  #Solo puede ser de America del sur o del norte
  {?country wdt:P361 wd:Q18} UNION {?country wdt:P361 wd:Q49} .
  #Nombres
  ?continent wdt:P31 wd:Q5107 ;
             rdfs:label ?continent_name .
  ?country wdt:P31 wd:Q6256 ;
             rdfs:label ?country_name .
  ?language rdfs:label ?language_name .
  #Coordenadas del lenguaje
  OPTIONAL {?language wdt:P625 ?coordinates}
  #Filtros de nombre
  FILTER(lang(?country_name)='en')
  FILTER(lang(?language_name)='en')
  FILTER(lang(?continent_name)='en')
  #Quitar lenguajes que se hablen en Europa (o Union Europea)
  FILTER(NOT EXISTS {
    ?country_2 wdt:P2936 ?language .
    {?country_2 wdt:P361 wd:Q46} UNION {?country_2 wdt:P361 wd:Q458} UNION {?country_2 wdt:P30 wd:Q48} .
  })

  ?language wdt:P1098 ?speakers .
}
GROUP BY (?language_name)
ORDER BY DESC (?total_speakers)